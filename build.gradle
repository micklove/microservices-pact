buildscript {
    ext {
        springBootVersion = '1.2.3.RELEASE'
    }
    repositories {
        mavenCentral()

    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'au.com.dius:pact-jvm-provider-gradle_2.11:2.1.7'
    }
}

allprojects {
  apply plugin: 'java'
  apply plugin: 'idea'

  repositories {
      mavenCentral()
  }
}

subprojects {
  apply plugin: 'spring-boot'


  //ensure JAVA_HOME is using 1.8 version
  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  dependencies {
    compile("org.springframework.boot:spring-boot-starter-web")
    testCompile("org.springframework.boot:spring-boot-starter-test")
    testCompile("au.com.dius:pact-jvm-consumer-junit_2.11:2.1.7")
  }
}

project(':microservices-pact-consumer') {
  jar {
      baseName = 'microservices-pact-consumer'
      version = '0.0.1-SNAPSHOT'
  }
}

project(':microservices-pact-provider') {
  apply plugin: 'au.com.dius.pact'

  jar {
      baseName = 'microservices-pact-provider'
      version = '0.0.1-SNAPSHOT'
  }

  pact {
      serviceProviders {
          fooProvider {
              hasPactWith('fooConsumer') {
                  pactFile = file("${project(':microservices-pact-consumer').projectDir}/target/pacts/Foo_Consumer-Foo_Provider.json")
              }
          }
      }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}
